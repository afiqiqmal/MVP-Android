apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'io.fabric'
apply from : 'dependencies.gradle'
apply from : 'signing.gradle'
apply from : 'function.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def sumVersionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100;

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion


    defaultConfig {
        applicationId "com.java.mvp.mvpandroid"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode sumVersionCode
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    buildTypes.each{
        it.resValue "string", "facebook_app_id", (project.findProperty("MVP_FACEBOOK_ID")?:"")
        it.resValue "string", "facebook_auth", (project.findProperty("MVP_FACEBOOK_AUTH")?:"")
        it.resValue "string", "fb_login_protocol_scheme", (project.findProperty("MVP_FACEBOOK_PROTOCOL")?:"")
        it.resValue "string", "google_maps_key", getGoogleApi()
        it.buildConfigField 'String','URL_API',getEncryptKey(getApi())
    }

    buildTypes {

        debug {
            testCoverageEnabled true
            versionNameSuffix "-staging"
            minifyEnabled false
            buildConfigField 'String','URL_API',getEncryptKey(getApi())
        }

        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String','URL_API',getEncryptKey(getApi())
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def finalName = "mvp-" + variant.versionName + "-" + variant.productFlavors[0].name + "-" + variant.buildType.name + "-" + getDateTime() + ".apk";
                output.outputFile = new File(output.outputFile.parent, finalName)
            }
        }

        android.variantFilter { variant ->
            if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'mock') {
                variant.setIgnore(true);
            }
        }
    }

    productFlavors {
        mock {
            applicationId rootProject.packageNameMock
        }
        prod {
            applicationId rootProject.packageName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

configurations.all {
    resolutionStrategy {
        force libraries.supportAnnotations
    }
}

apply plugin: 'com.google.gms.google-services'